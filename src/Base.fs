module ElmishLand.Base

open System
open System.Diagnostics
open System.IO
open System.Reflection
open ElmishLand.Log

type DotnetSdkVersion = | DotnetSdkVersion of Version

module DotnetSdkVersion =
    let fromString (s: string) =
        match Version.TryParse s with
        | true, version -> Some(DotnetSdkVersion version)
        | _ -> None

    let value (DotnetSdkVersion version) = version
    let asString (DotnetSdkVersion version) = version.ToString()

    let asFrameworkVersion (DotnetSdkVersion version) =
        $"net%i{version.Major}.%i{version.Minor}"

let minimumRequiredDotnetSdk = DotnetSdkVersion(Version(6, 0, 100))

let minimumRequiredNode = Version(18, 0)

module String =
    let asLines (s: string) = s.Split(Environment.NewLine)

    let asKebabCase (s: string) =
        s.Replace(" ", "-").Replace("_", "-").ToLower()

let ofOption error =
    function
    | Some s -> Ok s
    | None -> Error error

type ResultBuilder() =
    member _.Return(x) = Ok x

    member _.ReturnFrom(m: Result<_, _>) = m

    member _.Bind(m, f) = Result.bind f m
    member _.Bind((m, error): (Option<'T> * 'E), f) = m |> ofOption error |> Result.bind f

    member _.Zero() = None

    member _.Combine(m, f) = Result.bind f m

    member _.Delay(f: unit -> _) = f

    member _.Run(f) = f ()

    member __.TryWith(m, h) =
        try
            __.ReturnFrom(m)
        with e ->
            h e

    member __.TryFinally(m, compensation) =
        try
            __.ReturnFrom(m)
        finally
            compensation ()

    member __.Using(res: #IDisposable, body) =
        __.TryFinally(
            body res,
            fun () ->
                match res with
                | null -> ()
                | disp -> disp.Dispose()
        )

    member __.While(guard, f) =
        if not (guard ()) then
            Ok()
        else
            do f () |> ignore
            __.While(guard, f)

    member __.For(sequence: seq<_>, body) =
        __.Using(sequence.GetEnumerator(), (fun enum -> __.While(enum.MoveNext, __.Delay(fun () -> body enum.Current))))

let result = ResultBuilder()

let appFilePath =
    lazy
        let location = Assembly.GetExecutingAssembly().Location

        let location =
            if String.IsNullOrEmpty location then
                Process.GetCurrentProcess().MainModule.FileName
            else
                location

        Log().Debug(location)
        location

let private fileVersionInfo =
    lazy
        let fvi = FileVersionInfo.GetVersionInfo(appFilePath.Value)
        let fvi = if fvi.FileVersion = null then None else Some fvi
        Log().Debug("{}", fvi)
        fvi

let isPreRelease =
    lazy
        match fileVersionInfo.Value with
        | Some fvi ->
            fvi.ProductVersion.Contains("-alpha.")
            || fvi.ProductVersion.Contains("-beta.")
            || fvi.ProductVersion.Contains("-rc.")
            || fvi.ProductVersion.Contains("+Branch.")
        | None -> true

let version =
    lazy
        match fileVersionInfo.Value with
        | Some fvi ->
            if fvi.ProductVersion.Contains("+") then
                fvi.ProductVersion[0 .. fvi.ProductVersion.IndexOf("+") - 1]
            else
                fvi.ProductVersion
        | None -> "unknown"

let appTitle = lazy $"Elmish Land (v%s{version.Value})"

let welcomeTitle =
    lazy
        let title = $"Welcome to %s{appTitle.Value}"

        $"""
%s{title}
%s{String.init title.Length (fun _ -> "-")}
"""

let cliName = "dotnet elmish-land"

let help eachLine =
    $"""Here are the available commands:

%s{cliName} init ...... create a new project in the current directory
%s{cliName} upgrade ....... upgrade project to the latest elmish-land
%s{cliName} server ........................... run a local dev server
%s{cliName} build ..................... build your app for production
%s{cliName} add page <url> ........................... add a new page
%s{cliName} add layout <name> ...................... add a new layout
%s{cliName} routes ...................... list all routes in your app

Want to learn more? Visit https://elmish.land
"""
    |> _.Split(Environment.NewLine)
    |> Array.map eachLine
    |> String.concat Environment.NewLine

let autogenerated =
    lazy
        $"""//------------------------------------------------------------------------------
// <auto-generated>
// This code was generated by %s{appTitle.Value}.
%s{help (fun s -> $"// %s{s}")}
// This file will be overwritten when re-executing
// '%s{cliName} server' or '%s{cliName} build'.
// </auto-generated>
//------------------------------------------------------------------------------"""

let getTemplatesDir =
    Path.Combine(Path.GetDirectoryName(appFilePath.Value), "src", "templates")

let getProjectDir (projectName: string) =
    Path.Combine(Environment.CurrentDirectory, projectName)

let getProjectPath workingDirectory =
    let projectDir =
        match workingDirectory with
        | Some workingDirectory' -> Path.Combine(Environment.CurrentDirectory, workingDirectory')
        | None -> Environment.CurrentDirectory

    Path.ChangeExtension(Path.Combine(projectDir, DirectoryInfo(projectDir).Name), "fsproj")

let commandHeader s =
    let header = $"%s{appTitle.Value} %s{s}"

    $"""
%s{header}
%s{String.init header.Length (fun _ -> "-")}
"""

let canonicalizePath (path: string) = path.Replace("\\", "/")

type FilePath = private | FilePath of string

let (|FilePath|) (FilePath filePath) = filePath

type AbsoluteProjectDir = private | AbsoluteProjectDir of FilePath

type FileName = private | FileName of string

module FilePath =
    let fromString (filePath: string) = (canonicalizePath filePath) |> FilePath
    let extension (FilePath filePath) = Path.GetExtension(filePath)

    let ensureRelativeTo (AbsoluteProjectDir(FilePath projectDir)) (FilePath filePath) =
        filePath.Replace($"%s{projectDir}/", "") |> FilePath

    let directoryPath (FilePath filePath) =
        Path.GetDirectoryName(filePath) |> FilePath

    let asString (FilePath filePath) = filePath

    let equals (FilePath filePath1) (FilePath filePath2) =
        filePath1.Equals(filePath2, StringComparison.InvariantCulture)

    let appendParts append (FilePath basePath) =
        let appendPath = append |> String.concat "/"
        $"%s{basePath}/%s{appendPath}" |> FilePath

    let appendFilePath append (FilePath basePath) =
        $"%s{basePath}/%s{asString append}" |> FilePath

    let startsWithParts parts (FilePath path) =
        path.StartsWith(parts |> String.concat "/")

    let endsWithParts parts (FilePath path) =
        path.EndsWith(parts |> String.concat "/")

    let exists (AbsoluteProjectDir projectDir) parts =
        File.Exists(projectDir |> appendParts parts |> asString)

module AbsoluteProjectDir =
    let private currentDirectory =
        lazy
            Log().Debug(Environment.CurrentDirectory)

            canonicalizePath Environment.CurrentDirectory
            |> Path.TrimEndingDirectorySeparator
            |> FilePath

    let defaultProjectDir =
        FilePath.asString currentDirectory.Value |> FilePath |> AbsoluteProjectDir

    let fromFilePath (FilePath absoluteOrRelativeFilePath) =
        if Path.IsPathRooted absoluteOrRelativeFilePath then
            Path.TrimEndingDirectorySeparator absoluteOrRelativeFilePath
        else
            $"%s{currentDirectory.Value |> FilePath.asString}/%s{Path.TrimEndingDirectorySeparator absoluteOrRelativeFilePath}"
        |> FilePath
        |> AbsoluteProjectDir

    let asFilePath (AbsoluteProjectDir projectDir) = projectDir
    let asString (AbsoluteProjectDir(FilePath projectDir)) = projectDir


module FileName =
    let fromFilePath (FilePath filePath) = Path.GetFileName(filePath) |> FileName
    let asString (FileName fileName) = fileName

    let equalsString s (FileName fileName) =
        fileName.Equals(s, StringComparison.InvariantCulture)

let (|AbsoluteProjectDir|) (AbsoluteProjectDir projectDir) = projectDir

type FsProjPath =
    private
    | FsProjPath of FilePath

    static member fromProjectDir(AbsoluteProjectDir(FilePath projectDir)) =
        Path.ChangeExtension($"%s{projectDir}/%s{DirectoryInfo(projectDir).Name}", "fsproj")
        |> FilePath
        |> FsProjPath

    static member asString(FsProjPath(FilePath str)) = str
    static member asFilePath(FsProjPath filePath) = filePath

let (|FsProjPath|) (FsProjPath projectPath) = projectPath

type ProjectName =
    private
    | ProjectName of string

    static member fromProjectDir(AbsoluteProjectDir projectDir) =
        projectDir |> FileName.fromFilePath |> FileName.asString |> ProjectName

    static member asString(ProjectName projectName) = projectName

let writeResource (projectDir: AbsoluteProjectDir) overwrite (name: string) dst replace =
    let dstPath = AbsoluteProjectDir.asFilePath projectDir |> FilePath.appendParts dst

    if overwrite || not (File.Exists(FilePath.asString dstPath)) then
        let dstDir = FilePath.directoryPath dstPath |> FilePath.asString

        if not (Directory.Exists dstDir) then
            Directory.CreateDirectory(dstDir) |> ignore

        let assembly = Assembly.GetExecutingAssembly()
        let resourceName = $"templates.%s{name}"

        Log()
            .Debug("Writing resource '{}' to '{}'", resourceName, FilePath.asString dstPath)

        use stream = assembly.GetManifestResourceStream($"templates.%s{name}")
        use reader = new StreamReader(stream)
        let fileContents = reader.ReadToEnd()
        File.WriteAllText(FilePath.asString dstPath, fileContents)

        match replace with
        | Some f ->
            File.ReadAllText(FilePath.asString dstPath)
            |> f
            |> (fun x -> File.WriteAllText(FilePath.asString dstPath, x))
        | None -> ()

let dotnetToolDependencies = [
    "fable", "--version 4.*"
    "elmish-land", (if isPreRelease.Value then "--prerelease" else "")
]

let nugetDependencies = [
    "Elmish", "--version 4.*"
    "Fable.Elmish.HMR", "--version 7.*"
    "Fable.Elmish.React", "--version 4.*"
    "Feliz", "--version 2.*"
    "Feliz.Router", "--version 4.*"
    "Elmish", "--version 4.*"
]

let npmDependencies = [ "react", "18"; "react-dom", "18" ]

let npmDevDependencies = [ "vite", "5" ]

let dependencyCommands = [
    for name, version in nugetDependencies do
        "dotnet", [| "add"; "package"; name; version |]
    for name, version in npmDependencies do
        "npm", [| "install"; $"%s{name}@%s{version}"; "--save" |]
    for name, version in npmDevDependencies do
        "npm", [| "install"; $"%s{name}@%s{version}"; "--save-dev" |]
]
